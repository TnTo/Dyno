import sys

AddOption('--test', action='store_true', help='output coverage files')
AddOption('--debug-build', action='store_true', help='debug mode')

env = Environment(CPPPATH=['.', 'Dyno'])

env.Append(CXXFLAGS=['-Wall'])

if GetOption('test'):
    env.Append(CXXFLAGS=['--coverage'], LIBS=['gcov'])

if GetOption('debug_build'):
    env.Append(CXXFLAGS=['-g', '-ggdb'])
else:
    env.Append(CXXFLAGS=['-O2'], LINKFLAGS=['-O2'])

build_path_relative_to_sconstruct = Dir('..').path

conan = SConscript(
    '{}/SConscript_conan'.format(build_path_relative_to_sconstruct))
if not conan:
    print('File `SConscript_conan` is missing.')
    print('It should be generated by running `conan install`.')
    sys.exit(1)

flags = conan["conan"]
env.MergeFlags(flags)

env.Library('Dyno', ['Dyno.h'] + Glob('Dyno/*.cpp'))
